// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.4.2' apply false
    id 'com.android.library' version '7.4.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
    id 'com.google.gms.google-services' version '4.3.15' apply false
}

allprojects {
    ext {
// NOTE: replace the value with a token specific to your application
        cameraKitApiToken = property('com.snap.camerakit.api.token')
        // NOTE: replace the value with a lenses group ID specific to your application
        cameraKitLensesGroupId = property('com.snap.camerakit.lenses.group.id')
        cameraKitLensesGroupIdArCore = property('com.snap.camerakit.lenses.group.id.arcore')
        exoPlayerVersion = '2.16.1'
        gmsVisionVersion = '16.2.0'
    }
}

subprojects {
    // AS has a bug where it incorrectly constructs a dependency version string for an artifact that
    // is published with .aar extension (Maven packaging) when executing a "download sources" task
    // that is built-in, see similar issue: https://youtrack.jetbrains.com/issue/IDEA-234036.
    // We fix it for Camera Kit artifacts by removing @aar extension from version name using
    // a custom resolutionStrategy:
    configurations.all {
        if (it.name.startsWith('downloadSources')) {
            resolutionStrategy {
                eachDependency { DependencyResolveDetails details ->
                    if (details.requested.group == 'com.snap.camerakit') {
                        details.useVersion details.requested.version
                                .replaceAll("@aar", "")
                        // Version might get appended with " (1)", " (2)" etc. which also
                        // need to be removed:
                                .split(" ")
                                .first()
                    }
                }
            }
        }
    }

    tasks.whenTaskAdded { task ->
        if (task.class.name.contains('PackagedDependenciesWriterTask')) {
            task.outputs.doNotCacheIf('https://issuetracker.google.com/196847356')  {
                true
            }
        }
    }

//    apply plugin: libs.plugins.ktlint.get().pluginId
}